function void check_version_of_extra_package (void)
    /* Need the splitfname() function from the 'extra' package. */

    pkg query extra --quiet
    if $result.version < 0.6
        catch pkg install extra
        errorif($error > 0, "Failed to install the 'extra' package of minimum version 0.6")
    endif
end function


function void set_grid_dimension (scalar *n_rows,
                                  scalar *n_cols,
                                  const int N)
    /* Set number of rows and columns for grid. */

    if n_rows * n_cols > 0
        if N > (n_rows * n_cols)	# fully manual spec
            funcerr(sprintf("The specified grid (%d by %d) is insufficient for the number of\nsub-plots to plot (%d).", n_rows, n_cols, N))
        elif N < (n_rows * n_cols)
            funcerr(sprintf("The specified grid (%d by %d) exceeds the number of\nsub-plots to plot (%d).", n_rows, n_cols, N))
        endif
    elif n_rows && !n_cols
        n_cols = ceil(N / n_rows)
    elif !n_rows && n_cols
        n_rows = ceil(N / n_cols)
    elif !n_rows && !n_cols			# fully automatic specification
        n_rows = ceil(sqrt(N))
        n_cols = ceil(N / n_rows)
    endif
end function


function bundle get_default_parameters (void)
    /* Set default plotting parameters here. */

    return defbundle(\
      "PLOT_WIDTH", 800,\
      "PLOT_HEIGHT", 600,\
      "FONT_SIZE", 12)
end function


function void write_terminal_and_mplot_dims (const string terminal,
                                             const int n_rows,
                                             const int n_cols,
                                             const bundle options)
    /* Set canvas and multiplot dimensions for grid. */

    set_canvas(terminal, options)
    printf "set multiplot layout %d,%d rowsfirst\n", n_rows, n_cols
end function


function void set_canvas (const string terminal,
                          const bundle options)
    /* Actual writing of the terminal command: Set size of canvas and font size.*/

    scalar factor = 1
    factor = (terminal == "pdfcairo" || terminal == "epscairo") ? 100 : factor

    printf "set term %s font ',%d' size %d,%d\n", terminal, options.FONT_SIZE, options.PLOT_WIDTH / factor, options.PLOT_HEIGHT / factor
end function


function string compile_and_write_joint_code (const string terminal,
                                              const strings files_input,
                                              const int n_rows,
                                              const int n_cols,
                                              const bundle options)
    /* Open temporary file, write gnuplot stuff and clode file. */

    string file_tmp
    outfile --tempfile=file_tmp
        write_terminal_and_mplot_dims(terminal, n_rows, n_cols, options)
        read_input_files_write_to_tmp(files_input)
        print "unset multiplot"
    end outfile

    return file_tmp
end function


function void read_input_files_write_to_tmp (const strings files_input)
/* Read in gp-files of separate plots, and write content
    to common gnuplot file. */

    scalar N = nelem(files_input)
    string error_string = ""

    loop i=1..N --quiet
        catch string file_content = readfile(files_input[i])
        if $error
            error_string ~= sprintf("Warning: Error reading file %s. Ignored.\n", files_input[i])
        else
            printf "%s\n", file_content
            printf "reset\n"  # reset to default values after each plot
        endif
    endloop

    if nelem(error_string)
        print error_string
    endif
end function


function string set_terminal (const string file_type)
    /* Determine the gnuplot terminal identifier name. */

    if file_type == "png"
        string terminal = "png"
    elif file_type == "pdf"
        string terminal = "pdfcairo"
    elif file_type == "eps"
        string terminal = "epscairo"
    elif file_type == "svg"
        string terminal = "svg"
    else
        funcerr(sprintf("Error: Requested terminal '%s' is not supported.\n"))
    endif

    return terminal
end function


function string multiplot (const strings files_input "Input file paths/names",
                           const string file_output[null] "Full output file path/name",
                           int n_rows[0::0] "Number of rows in plot grid",
                           int n_cols[0::0] "Number of cols in plot grid",
                           bundle options[null])
    /* Main public function.
       return: String containing the gnuplot plot code. */

    check_version_of_extra_package()

    # Initializations
    if !exists(file_output) || (exists(file_output) && !strlen(file_output))
        string file_output = "display"
        string file_type = "png"
    else
        string file_type = splitfname(file_output)[3]
    endif
    if exists(options)
        bundle options = options + get_default_parameters()
    else
        bundle options = get_default_parameters()
    endif

    string terminal = set_terminal(file_type)
    set_grid_dimension(&n_rows, &n_cols, nelem(files_input))
    string file_tmp = compile_and_write_joint_code(terminal, files_input, n_rows, n_cols, options)

    gnuplot --input="@file_tmp" --output="@file_output"

    return readfile(file_tmp)
end function
